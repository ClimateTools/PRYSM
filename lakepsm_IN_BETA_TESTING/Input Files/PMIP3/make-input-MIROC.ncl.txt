load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

begin

;***************************
; set parameters
;***************************
 
 dir = "/glade/p/work/morrill/DATA-FILES/"  ; directory for PMIP3 files
 slice = "HIST"   ; set to either PI, LGM, MH, HIST 
 area = "Mcore"  ; set either Mcore, Tcore, Mlake, Tlake

;*********************
;  set grid cells and years
;*********************

  if (area.eq."Mcore") then
    xlat = (/28/)
    xlon = (/12/)
  end if
  if (area.eq."Tcore") then
    xlat = (/29/)
    xlon = (/11/)
  end if
  if (area.eq."Mlake") then
    xlat = (/28,27/)
    xlon = (/12,12/)
  end if
  if (area.eq."Tlake") then
    xlat = (/30,29/)
    xlon = (/10,11/)
  end if

  if (slice.eq."LGM".or.slice.eq."MH".or.slice.eq."PI") then
    nyr = 100
  end if
  if (slice.eq."HIST") then
    nyr = 156
  end if

;********************
; get data
;********************

  njul = (/31,28,31,30,31,30,31,31,30,31,30,31/) ; # julian days/month

  f8 = addfile(dir+"sftgif_fx_MIROC-ESM_lgm_r0i0p0.nc","r")  ; glacier fraction
  f9 = addfile(dir+"sftlf_fx_MIROC-ESM_piControl_r0i0p0.nc","r") ; land fraction
  sftgif = f8->sftgif
  sftlf = f9->sftlf

  nmon = nyr*12 
  allvars = new((/nmon,8/),"float")

  if (slice.eq."LGM") then
    f1 = addfile(dir+"tas_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
    f2 = addfile(dir+"huss_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
    f3 = addfile(dir+"sfcWind_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
    f4 = addfile(dir+"rsds_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
    f5 = addfile(dir+"rlds_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
    f6 = addfile(dir+"ps_Amon_MIROC-ESM_lgm_r1i1p1_460001-469912.nc","r")
  end if
  if (slice.eq."PI") then
    f1 = addfile(dir+"tas_Amon_MIROC-ESM_piControl_r1i1p1_180001-189912.nc","r")
    f2 = addfile(dir+"huss_Amon_MIROC-ESM_piControl_r1i1p1_180001-233012.nc","r")
    f3 = addfile(dir+"sfcWind_Amon_MIROC-ESM_piControl_r1i1p1_180001-233012.nc","r")
    f4 = addfile(dir+"rsds_Amon_MIROC-ESM_piControl_r1i1p1_180001-233012.nc","r")
    f5 = addfile(dir+"rlds_Amon_MIROC-ESM_piControl_r1i1p1_180001-233012.nc","r")
    f6 = addfile(dir+"ps_Amon_MIROC-ESM_piControl_r1i1p1_180001-233012.nc","r")
  end if
  if (slice.eq."MH") then
    f1 = addfile(dir+"tas_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
    f2 = addfile(dir+"huss_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
    f3 = addfile(dir+"sfcWind_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
    f4 = addfile(dir+"rsds_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
    f5 = addfile(dir+"rlds_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
    f6 = addfile(dir+"ps_Amon_MIROC-ESM_midHolocene_r1i1p1_233001-242912.nc","r")
  end if
  if (slice.eq."HIST") then
    f1 = addfile(dir+"tas_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
    f2 = addfile(dir+"huss_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
    f3 = addfile(dir+"sfcWind_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
    f4 = addfile(dir+"rsds_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
    f5 = addfile(dir+"rlds_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
    f6 = addfile(dir+"ps_Amon_MIROC-ESM_historical_ensemble_185001-200512.nc","r")
  end if

  var2 = f1->tas(0:(nmon-1),:,:) 
  var3 = f2->huss(0:(nmon-1),:,:)
  var4 = f3->sfcWind(0:(nmon-1),:,:)
  var5 = f4->rsds(0:(nmon-1),:,:)
  var6 = f5->rlds(0:(nmon-1),:,:)
  var7 = f6->ps(0:(nmon-1),:,:)
  nlat = dimsizes(var2&lat)
  nlon = dimsizes(var2&lon)

;**************************************************
;  calculate averages for desired grid cells
;**************************************************

  mask_array = new((/nmon,nlat,nlon/),"float")
  copy_VarCoords(var2,mask_array)
  do j=0,dimsizes(xlat)-1
    mask_array(:,xlat(j),xlon(j)) = 1.
  end do

  sftgif = sftgif*dim_avg_n_Wrap(mask_array,0)
  sftlf = sftlf*dim_avg_n_Wrap(mask_array,0)
  if (any(sftgif.gt.0.or.sftlf.eq.0)) then
     print("ocean and/or glacier grid cells")
  end if
 
  allvars(:,2) = wgt_areaave(mask_array*var2,1.0,1.0,0)
  allvars(:,3) = wgt_areaave(mask_array*var3,1.0,1.0,0)
  allvars(:,4) = wgt_areaave(mask_array*var4,1.0,1.0,0)
  allvars(:,5) = wgt_areaave(mask_array*var5,1.0,1.0,0)
  allvars(:,6) = wgt_areaave(mask_array*var6,1.0,1.0,0)
  allvars(:,7) = wgt_areaave(mask_array*var7,1.0,1.0,0) 

;****************************************
;  calculate years and days
;****************************************

  ijul = 15
  do j = 1,nyr
    do k = 1,12
      ihold = (j-1)*12 + k-1
      allvars(ihold,0) = int2flt(j)   ; model year
      allvars(ihold,1) = int2flt(ijul) ; cumulative julian day
      ijul = ijul+njul(k-1)
     end do
  end do

;*****************************************
;  write out
;*****************************************

  allvars@_FillValue = -99.99
  fmtx = "f5.1,1x,f7.1,1x,f6.2,1x,f10.6,1x,f6.2,1x,f6.2,1x,f6.2,1x,f9.2"
  opt = True
  opt@fout = "MIROC-"+slice+"-"+area+".txt"
  write_matrix(allvars,fmtx,opt)

end
